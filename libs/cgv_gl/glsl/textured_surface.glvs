#version 330

uniform mat4 texture_matrix = mat4(
	vec4(1.0, 0.0, 0.0, 0.0), 
	vec4(0.0, 1.0, 0.0, 0.0), 
	vec4(0.0, 0.0, 1.0, 0.0), 
	vec4(0.0, 0.0, 0.0, 1.0) 
);

uniform bool use_texture_matrix = false;

//***** begin interface of view.glsl ***********************************
mat4 get_modelview_matrix();
mat4 get_modelview_projection_matrix();
mat3 get_normal_matrix();
//***** end interface of view.glsl ***********************************

layout(location = 0) in vec4 position;
layout(location = 1) in vec4 color;
layout(location = 2) in vec3 normal;
layout(location = 3) in vec2 texcoord;

out vec3 normal_eye;
out vec3 position_eye;
out vec4 color_fs;
out vec2 texcoord_fs;

void main()
{
	// transform vertex to clip space
	gl_Position = get_modelview_projection_matrix()*position;
	vec4 p = get_modelview_matrix()*position;
	position_eye = p.xyz / p.w;

	normal_eye = get_normal_matrix()*normal;
	color_fs = color;
	if (use_texture_matrix) {
		vec4 ht = texture_matrix * vec4(texcoord, 0.0, 1.0);
		texcoord_fs = ht.xy / ht.w;
	}
	else
		texcoord_fs = texcoord;
}
